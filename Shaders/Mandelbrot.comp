#version 460 core

layout( push_constant ) uniform constants
{
    double x, y, scale;
    //uint max_iter;
};

const int max_iter = 512;

layout( set=0, binding=0, rgba8 ) uniform image2D image;

layout( constant_id = 0 ) const int variant = 2;

vec3 hsv2rgb(vec3 c)
{
    const vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

vec4 makeColor( uint n )
{
    vec3 hsv = vec3( float(n) / float(max_iter), 1., float( n < max_iter ) );
    return vec4( hsv2rgb( hsv ), 1. );
}

uvec4 getLastToInfParalel( dvec4 c )
{
    const dvec4 minInf = dvec4( 4. );

    dvec4 re = dvec4( 0. );
    dvec4 im = dvec4( 0. );

    dvec4 resq = re*re;
    dvec4 imsq = im*im;

    uint N = 0;
    uvec4 n = uvec4( 0 );

    bvec4 act;

    do
    {
        im = 2*re*im + c.zwzw;
        re = resq - imsq + c.xxyy;
        
        resq = re*re;
        imsq = im*im;

        act = lessThanEqual(resq + imsq, minInf );
        n += uvec4( act );
        N++;
    }
    while( any( act ) && N < max_iter );

    return n;
}

uint getLastToInf( dvec2 c )
{
    const dvec2 oneNegOne= { 1, -1 };
    const double minInf = 4.;
    
    dvec2 z = { 0, 0 };

    uint n = 0;
    do
    {
        z = dvec2( dot( z, oneNegOne * z ), 2*z.x*z.y) + c;

        n++;
    }
    while( dot(z, z) <= minInf && n < max_iter );

    return n;
}

uint getLastToInfV3( dvec2 c )
{
    const double minInf = 4.;
    
    dvec2 z = { 0, 0 };
    dvec2 zsq = z*z;

    uint n = 0;

    do
    {
        z = dvec2( zsq.x - zsq.y, 2*z.x*z.y ) + c;        
        zsq = z*z;

        n++;
    }
    while( zsq.x + zsq.y <= minInf && n < max_iter );

    return n;
}

layout(local_size_x = 8, local_size_y = 8) in;

void main()
{
    ivec2 screenPos = ivec2(gl_GlobalInvocationID.xy) * (variant == 0 ? 2 : 1);
    ivec2 texSize = imageSize(image);

    if( any( greaterThanEqual( screenPos, texSize ) ) ) return;

    ivec2 imageOff = screenPos - texSize / 2;

    dvec2 basePos = dvec2(x, y) + imageOff * scale;

    if( variant == 0 )
    {
        dvec4 c = basePos.xxyy;
        c.yw += scale;

        uvec4 n = getLastToInfParalel( c );

        const ivec2 offsets[4] = {
            { 0, 0 },
            { 0, 1 },
            { 1, 0 },
            { 1, 1 }
        }; 

        for( int i = 0; i < 4; i++ )
        {
            ivec2 pos = screenPos + offsets[i];
            if( all( lessThan( pos, texSize ) ) )
                imageStore(image, pos, makeColor( n[i] ) );
        }
    }
    else imageStore(image, screenPos, 
        makeColor( variant == 1 ? getLastToInf( basePos ) : getLastToInfV3( basePos ) ) );
}
